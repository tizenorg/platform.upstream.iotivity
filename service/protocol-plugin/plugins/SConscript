#******************************************************************
#
# Copyright 2014 Samsung Electronics All Rights Reserved.
#
#-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
#-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
##
# Plugins build script
##

Import('env')

# Add third party libraries
lib_env = env.Clone()
SConscript(env.get('SRC_DIR') + '/service/third_party_libs.scons', 'lib_env')

plugins_env = lib_env.Clone()

target_os = env.get('TARGET_OS')
######################################################################
# Build flags
######################################################################
plugins_env.AppendUnique(CPPPATH = ['../lib/cpluff/libcpluff'])

if target_os not in ['windows', 'winrt']:
    plugins_env.AppendUnique(CXXFLAGS = ['-g3', '-Wall', '-pthread', '-std=c++0x'])
    plugins_env.PrependUnique(CCFLAGS = ['-fPIC'])
    plugins_env.AppendUnique(LINKFLAGS = ['-fPIC'])

#plugins_env.AppendUnique(LIBS=['libconnectivity-abstraction'])

    if target_os not in ['arduino', 'android']:
        plugins_env.AppendUnique(LIBS = ['pthread'])

if target_os == 'android':
    plugins_env.AppendUnique(CXXFLAGS = ['-frtti', '-fexceptions'])
    plugins_env.AppendUnique(LIBS = ['gnustl_shared'])

plugins_env.AppendUnique(LIBS = [File(env.get('BUILD_DIR') + '/libmosquitto.a'),
        'mosquitto', 'ssl', 'rt'])


######################################################################
# Source files and Targets
######################################################################
plugins_env['LIBPREFIX'] = ''

mqtt_fan_src = Glob('mqtt-fan/src/' + '*.cpp')
fanserver = plugins_env.SharedLibrary('mqtt-fan/fanserver_mqtt_plugin', mqtt_fan_src)

mqtt_light_src = Glob('mqtt-light/src/' + '*.cpp')
lightserver = plugins_env.SharedLibrary('mqtt-light/lightserver_mqtt_plugin', mqtt_light_src)

SConscript('mqtt-fan/lib/SConscript')
